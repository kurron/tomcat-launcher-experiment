import org.kurron.tomcat.ShutdownMonitor
import org.kurron.tomcat.Tomcat7xServer
import org.kurron.tomcat.TomcatServer

apply plugin: 'groovy'
apply plugin: 'project-report'

defaultTasks 'clean', 'projectReport', 'build', 'ronbo'

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

apply plugin: TomcatPlugin

task ronbo( dependsOn: startTomcat ) << {
    println 'Running tests....'
    Thread.sleep( 1000 * 30 )
}
ronbo.finalizedBy = ['stopTomcat']

tomcat {
    port = 8080
    context = '/rest-services'
    warFile = new File('rest-services-1.0.0-SNAPSHOT.war')
    daemon = true
}

class TomcatPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.extensions.create("tomcat", TomcatPluginExtension)
        project.task('startTomcat') << {
            logger.quiet "Starting Tomcat on port ${project.tomcat.port} and context ${project.tomcat.context}"
            TomcatServer server = new Tomcat7xServer()
            server.port = project.tomcat.port
            String path = project.tomcat.warFile.canonicalPath
            logger.quiet "path = ${path}"
            server.createContext(project.tomcat.context, path)
            server.createLoader(Thread.currentThread().contextClassLoader)
            server.start()

            Thread shutdownMonitor = new ShutdownMonitor( project.tomcat.stopPort,  project.tomcat.stopKey, server,  project.tomcat.daemon )
            shutdownMonitor.start()

            if (!project.tomcat.daemon) {
                shutdownMonitor.join()
            }
        }
        project.task('stopTomcat') << {
            logger.quiet "Stopping Tomcat on port ${project.tomcat.stopPort} and key ${project.tomcat.stopKey}"
            Socket s = new Socket(InetAddress.getByName('127.0.0.1'), project.tomcat.stopPort)
            s.setSoLinger(false, 0)

            OutputStream out = s.outputStream
            out.write((project.tomcat.stopKey + '\r\nstop\r\n').bytes)
            out.flush()
            s.close()
        }
    }
}

class TomcatPluginExtension {
    int port = 8080
    int stopPort = 8081
    String stopKey = 'stop-key'
    boolean daemon = false
    File warFile
    String context = '/'
}
