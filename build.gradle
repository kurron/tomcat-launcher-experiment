apply plugin: 'groovy'
apply plugin: 'project-report'

ext {
    tomcatVersion = '7.0.42'
    groovyVersion = '2.1.7'
    hamcrestVersion = '1.3'
    junitVersion = '4.11'
    logbackVersion = '1.0.13'
    slf4jVersion = '1.7.5'
    spockVersion = '0.7-groovy-2.0'
}

defaultTasks 'clean', 'projectReport', 'build'

repositories {
    mavenCentral()
}

configurations.all {
    exclude group: 'commons-logging'
    exclude group: 'log4j'
    exclude module: 'junit-dep'
    exclude module: 'hamcrest-core'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:slf4j-ext:${slf4jVersion}"
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    compile "ch.qos.logback:logback-core:${logbackVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jasper:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jasper-el:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-jsp-api:${tomcatVersion}"

    runtime "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    runtime "org.slf4j:log4j-over-slf4j:${slf4jVersion}"

    testCompile "org.spockframework:spock-core:${spockVersion}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

apply plugin: GreetingPlugin

greeting {
    message = 'Hi'
    greeter = 'Gradle'
}

class GreetingPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.extensions.create("greeting", GreetingPluginExtension)
        project.task('hello') << {
            println "${project.greeting.message} from ${project.greeting.greeter}"
        }
    }
}

class GreetingPluginExtension {
    String message
    String greeter
}
